#compdef registers

local curcontext=$curcontext context state state_descr line opt_args

_arguments -C -s -S -A "-*" : \
    "(- : *)-h[show the help text]" \
    '1::action:((
        "read:output register content to stdout"
        "write:set register content from stdin"
        "append:add to register content from stdin"
        "clear:clear data from register"
    ))' \
    "*:: :->action" \
    && return

case $state in
action)
    service=$words[1]
    curcontext=${curcontext%:*}-$service

    local register_dir=${TMPDIR:/tmp}/registers.$USER
    local -a register_files=($register_dir/**(N.))
    _values 'registers' default ${register_files#$register_dir/}
    ;;
esac

#compdef secrets

local state state_descr context line opt_args

_arguments -s -S -A "-*" : \
    "(- : *)-h[show the help text]" \
    "(-h)-D[set store directory]:store directory:_files -/" \
    "(-h)-i[set identities file]:identities file:_files" \
    '(-h)::action:((
        "get:print secret to stdout"
        "set:set secret from stdin"
        "remove:remove secret"
    ))' \
    "(-h)::secret:->secret" \
    && return

if [[ $state == secret ]]; then
    local data_dir
    if (( $+opt_args[-D] )); then
        data_dir=$opt_args[-D]
    else
        data_dir=${SECRETS_DATA_DIR:-${XDG_DATA_HOME:-$HOME/.local/share}/secrets}
    fi
    local store_dir=$data_dir/store
    local -a age_files=($store_dir/**.age(N.))
    (($#age_files)) && _values 'secrets' ${${age_files#$store_dir/}%.age}
fi

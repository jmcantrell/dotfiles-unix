#compdef secrets

local curcontext=$curcontext context state state_descr line opt_args

_arguments -C -s -S -A "-*" : \
    "(- : *)-h[show the help text]" \
    "-D[set store directory]:store directory:_files -/" \
    "-i[set identities file]:identities file:_files" \
    '1::action:((
        "get:print secret to stdout"
        "set:set secret from stdin"
        "remove:remove secret"
    ))' \
    "*:: :->action" \
    && return

case $state in
action)
    service=$words[1]
    curcontext=${curcontext%:*}-$service

    local data_dir
    if (( $+opt_args[-D] )); then
        data_dir=$opt_args[-D]
    else
        data_dir=${SECRETS_DATA_DIR:-${XDG_DATA_HOME:-$HOME/.local/share}/secrets}
    fi

    local store_dir=$data_dir/store
    local -a age_files=($store_dir/**.age(N.))
    (($#age_files)) && _values 'secrets' ${${age_files#$store_dir/}%.age}
    ;;
esac

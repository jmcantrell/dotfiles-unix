setopt prompt_subst

set_env_info() {
    unset env_info
    local env_names=()
    git rev-parse &>/dev/null && env_names+=(git)
    [[ -v DIRENV_DIR ]] && env_names+=(direnv)
    [[ -v VIRTUAL_ENV ]] && env_names+=(venv)
    [[ -f package.json || -d node_modules ]] && env_names+=(node)
    [[ -f deno.json ]] && env_names+=(deno)
    [[ -f Cargo.toml ]] && env_names+=(cargo)
    [[ -r stack.yaml ]] && env_names+=(stack)
    [[ -n *.cabal(#qN) ]] && env_names+=(cabal)
    (( $#env_names )) && env_info="%F{3}${env_names[*]}%f"
}
precmd_functions+=set_env_info

# the current working directory
PS1='%U%F{7}%d%f%u'

# development environment(s)
PS1+='${env_info:+ $env_info}'

PS1+=$'\n'

# hostname (only in certain cases)
if [[ -v SSH_CLIENT || ! ( -v TMUX || -v WAYLAND_DISPLAY || -v DISPLAY ) ]]; then
    PS1+='%m '
fi

# user privilege indicator
PS1+='%# '

# last exit code (if non-zero)
RPS1='%(?..%F{2}%? %f)'

# current time (24 hour)
RPS1+='%F{8}%*%f'

# bold
PS1=%B${PS1}%b
RPS1=%B${RPS1}%b

# vi:ft=zsh

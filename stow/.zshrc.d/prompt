setopt prompt_subst

env_info() {
    unset env_info
    local env_names=()
    [[ -n $DIRENV_DIR ]] && env_names+=(env)
    [[ -d node_modules ]] && env_names+=(node)
    [[ -n $VIRTUAL_ENV ]] && env_names+=(python)
    [[ -n $env_names ]] && env_info="%F{3}${env_names[*]}%f"
}
precmd_functions+=env_info

autoload -U vcs_info
formats="%c%u%F{5}%b%f"
zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' stagedstr "%F{2}+%f"
zstyle ':vcs_info:*' unstagedstr "%F{1}!%f"
zstyle ':vcs_info:*' formats "$formats "
zstyle ':vcs_info:*' actionformats "%a:$formats "
precmd_functions+=vcs_info

# The current working directory.
PS1='%U%F{7}%d%f%u'

# Indicate if any tooling environments are active.
PS1+='${env_info:+ $env_info}'

# Version control status.
PS1+='${vcs_info_msg_0_:+ $vcs_info_msg_0_}'

PS1+=$'\n'

# Only show the hostname if over ssh.
PS1+='${SSH_CLIENT:+%m }'

# An indicator of user privilege.
PS1+='%# '

# The last exit code, if non-zero.
RPS1='%(?..%F{2}%? %f)'

# Current time (24 hour).
RPS1+='%F{8}%*%f'

# Make it bold.
PS1=%B${PS1}%b
RPS1=%B${RPS1}%b

# vi:ft=zsh
